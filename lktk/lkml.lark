lkml: pair*
pair: CODEBLOCKKEY ":" CODEBLOCK? ";;"
    | IDENT ":" value
dict: "{" pair* "}"
named_dict: CNAME dict 
arr: "[" [(value | pair) ("," (value | pair))*] "]"
?value: arr
      | dict
      | named_dict
      | ESCAPED_STRING
      | NUMBER
      | IDENT
IDENT: [("+" | "-")] CNAME ("." CNAME)* ["*"]
COMMENT: /#.*/
// \S is needed cause 0 width regexp is not allowed here
CODEBLOCK: /\S(.|\n)*?(?=\s*;;)/
CODEBLOCKKEY: "expression" | "html" | /sql\w*/

%import common.CNAME
%import common.ESCAPED_STRING
%import common.NUMBER
%import common.WS
%ignore WS
%ignore COMMENT
