lkml: pair*
code_pair: CODEBLOCKKEY ":" CODEBLOCK? ";;"
value_pair: IDENT ":" value
?pair: code_pair | value_pair
dict: "{" pair* "}"
named_dict: CNAME dict 
arr: "[" [(value | pair) ("," (value | pair))*] "]"
?value: arr
      | dict
      | named_dict
      | ESCAPED_STRING
      | NUMBER
      | IDENT
IDENT: [/(\+|-)\s*/] CNAME (/\s*\.\s*/ CNAME)* [/\s*?\*/]
COMMENT: /#.*/
// \S is needed cause 0 width regexp is not allowed here
CODEBLOCK: /\S(.|\n)*?(?=\s*;;)/
CODEBLOCKKEY: "expression" | "html" | /sql\w*/

%import common.CNAME
%import common.ESCAPED_STRING
%import common.NUMBER
%import common.WS
%ignore WS
%ignore COMMENT
